
# Words that could be built in

# acb - move third item to second in stack
#   abc => acb
: acb cab swap ;

# bca - move top item to third in stack
#   abc => bca
: bca cab cab ;

# @++ - increment variable
#   variable-addr => ...   [but variable is incremented]
: @++
	dup	# var var
	@	# int var
	1 + # (int+1) var
	@=  # ...
;

# @-- - decrement variable
#   variable-addr => ...   [but variable is decremented]
: @--
	dup	# var var
	@	# int var
	1 - # (int+1) var
	@=  # ...
;


# dup2 - duplicate top two items of stack
#   abc => ababc
: dup2
	bab	# babc
	bab # ababc
;

: .$^ .$ pop ;

: ...
   "S= " .$^
   . " " .$^ bca
   . " " .$^ bca
   . " " .$^ bca
   nl
;


# string ... -> bool ...
: isEmpty$
	head$	# char string'
	swap pop # char
	==0		# bool
;


: head2$	# In: s2 s1 ...
	head$	# c2 s2' s1
	cab		# s1 c2 s2'
	head$ 	# c1 s1' c2 s2'
	cab	  	# c2 c1 s1' s2'
;

: ==$  # stack= s2 s1 ...
	head2$  # c2 c1 s1' s2'
	dup2	# c2 c1 c2 c1 s1' s2'
	dup2	# c2 c1 c2 c1 c2 c1 s1' s2'

	while
		==
		==0
		!
		&
	do
		# characters are == and not 0
		# c1 c2 c1 s1' s2'

		pop pop pop	# s1' s2'
		head2$		# c1 c2 s1' s2'
		dup2
		dup2		# c2 c1 c2 c1 c2 c1 s1' s2'
	od

	# c1 c2 c1 s1' s2'
	pop  		# c2 c1 s1' s2'

	==

	cba
	pop pop
;



var s2a_index
var s2a_array

# string_to_array - convert string to array
# Effect:  string arrayPtr => ...
#
: string_to_array
	s2a_index 0 @=

	swap		# array string
	s2a_array swap @=	# string

	head$   # c string'

	while
		dup 	# c c string'
		0
		!=
	do			# c string'
		s2a_array @ swap	# c array string'
		s2a_index @		# index c array string'
		swap	# c index array string'
		@+=		# string'

		s2a_index @++
		head$	# c string'
	od

	# c(0) string'
	s2a_array @		# array 0 string'
	s2a_index @		# index array 0 string'
	cab		# 0 index array string'
	@+=		# string'
	pop
;

# length$ - get length of string
#   string => int
: length$
	0 swap	# string count(0)
	head$	# c string count

	while
		0 !=	# string count
	do
		swap 1 +	# count' string
		swap	# string count'
		head$	# c string' count'
	od

	# string count
	pop
;


var repeat_array
var repeat_char

# repeat$ - repeats characters to create a string
#   character array length
: repeat$
	repeat_char swap @=		# [store repeat character]
	repeat_array swap @=	# [store the array]
	0			# index length

	while
		dup2	# index length index length
		!=		# bool index length
	do			# index length
		dup		# index index length
		repeat_array @ swap	# index array index length
		repeat_char @ 		# '_' index array index length
		@+=		# index length
		1 + 	# index' length
	od

							# index length
	repeat_array @ swap 0	# 0 index array length
	@+=			# length
	pop
;

#####

# include tenthUnit
: assertEqual
	dup2

	==
	if
		pop pop
	else
		"Error: values differ" .$ pop nl
		"  a=" .$ pop
		. pop nl
		"  b=" .$ pop
		. pop nl
	fi
;

: assertEqual$
	dup2

	==$
	if
		pop pop
	else
		"Error: string values differ" .$ pop nl
		"  a=" .$ pop
		.$ pop nl
		"  b=" .$ pop
		.$ pop nl
	fi
;


#####
#  On to Hangman...

# Clear the screen (VT100 commands)
: screen_clear esc "[2J" .$^ ;

# screen_move_to - move the cursor to the specified position
#  in: col row ... => ...
: screen_move_to swap esc "[" .$^ . pop ";" .$^ . pop "H" .$^ ;

#####

2 array title	# array of strings

: title_init
	title 0 "HANGMAN" @+=
	title 1 "" @+=
;

title_init

#####

8 array scaffold  # array of strings

: scaffold_init
	scaffold 0 "_____" @+=
	scaffold 1 "|/   |" @+=
	scaffold 2 "|" @+=
	scaffold 3 "|" @+=
	scaffold 4 "|" @+=
	scaffold 5 "|" @+=
	scaffold 6 "|" @+=
	scaffold 7 "" @+=
;

scaffold_init



# draw_at - moves to row/col and draws the string
#	col row string ... => ...
: draw_at
	screen_move_to
	.$^
;


var draw_routine
var draw_index
var draw_column
var draw_row

# Draw array of strings, one per line, starting in given col and row
#    routine_name col row array ... => ...
: draw
	draw_routine swap @=		# col row array
	draw_index 0 @=
	draw_column swap @=			# row array
	draw_row swap @=			# array

	while				# array
		dup				# array array
		draw_index @	# int array array
		@+				# string array

		dup				# string string array
		isEmpty$		# bool string array
		!				# bool string array
	do					# string array
		draw_index @	# index string array
		draw_row @		# row index string array
		+				# (index+row) string array
		draw_column @	# col (index+row) string array

		draw_routine @	# routine_name col row string array
		call			# array

		draw_index @++	# array
	od

	# string array
	pop pop
;

#######

# Game State

100 array clue

100 array answer

30 array wrong_guesses
var wrong_guesses_index


# Initialization
: wrong_guesses_init
	wrong_guesses_index 0 @=
	wrong_guesses 0 0 @+=
;

# game_init - saves answer string and creates initial clue
#  Note: 95=_
# Input: answer - top of stack
# Output: sets answerString
#
: game_init			# string
	wrong_guesses_init

	dup				# string string
	answer swap		# string answer string

	string_to_array	# string [and builds the answer array]

	length$		# length
	clue 		# array length
	95			# '_' array length
	repeat$
;


# game_over - is game over? ie answer is guessed
#	In: none
#
: game_over
	clue make$
	answer make$
	==$
;

# game_display - show game state
: game_display
	screen_clear

	title 2 35 "draw_at" draw

	clue make$ 4 35 draw_at

	scaffold 6 35 "draw_at" draw

	"Wrong guesses: " 15 20 draw_at

	wrong_guesses make$ 15 35 draw_at

	nl nl nl
;


# update_clue - adjust clue if character is found. Return true if character found.
#   ch ... => bool ...

var clue_index

: update_clue
	clue_index 0 @=		# ch
	0 swap				# ch result(0)

	while
		answer clue_index @ @+	# ch-ans ch result
		dup		# ch-ans ch-ans ch result
		0		# 0 ch-ans ch-ans ch result
		!=		# bool ch-ans ch result
	do			# ch-ans ch result
		bab		# ch ch-ans ch result
		==			# bool ch result
		if			# ch result
			dup		# ch ch result
			clue clue_index @ cab @+=  	# ch result
			swap pop 1 swap		# ch result(true)
		fi			# ch result
		clue_index @++
	od

			# ch-ans ch result
	pop pop	# result
;

# update_wrong_guess - stores character in list of wrong guesses
#   ch => ...
: update_wrong_guess
	wrong_guesses wrong_guesses_index @	# index wrong_guesses ch
	cab		# ch index wrong_guesses
	@+=		#

	wrong_guesses_index @++
;


# game_loop - given answer, run the game until game is over
#   readRoutine displayRoutine => ...
: game_loop
	while		# read display
		game_over	# bool read display
		!			# bool read display
	do			# read display
		dup		# read read display
		call	# ch read display
		dup		# ch ch read display
		update_clue	# bool ch read display
		!			# bool ch read display
		if			# ch read display
			update_wrong_guess # read display
		else		# ch read display
			pop		# read display
		fi

		bab		# display read display
		call	# read display
	od

	# read display
	pop pop
;


var body_draw_routine

# draw_head
#   draw_routine col row ... =>  ...
: draw_head
	body_draw_routine swap @=	# col row
	2 + 		# col+2 row
	"O"			# string col row
	bca			# col row string
	body_draw_routine @ call	 # [empty]
;


50 array test_draw_area
var test_draw_index

# test_draw_spy
# col row string => ...
: test_draw_spy
	swap
	test_draw_area test_draw_index @ cab 48 + @+=	# row (add '0')
	test_draw_index @++

	test_draw_area test_draw_index @ 45 @+=		# write '-'
	test_draw_index @++

	test_draw_area test_draw_index @ cab 48 + @+=	# col (add '0')
	test_draw_index @++

	test_draw_area test_draw_index @ 45 @+=		# write '-'
	test_draw_index @++

	head$
	test_draw_area test_draw_index @ cab @+=	# first char of string
	pop
	test_draw_index @++

	test_draw_area test_draw_index @ 126 @+=	# write '~'
;

: test_draw_spy_check " test_draw_spy_check " .$^
	1029
	test_draw_index 0 @=

	"*/" 2 4 test_draw_spy

	test_draw_area test_draw_index @ 0 @+=
	test_draw_area make$ "2-4-*~" assertEqual$
	1029 assertEqual
;



: test_draw_head " test_draw_head " .$^
	6738

	test_draw_index 0 @=

	1 2 "test_draw_spy" draw_head

	test_draw_area test_draw_index @ 0 @+=
	test_draw_area make$ "1-4-O~" assertEqual$

	6738 assertEqual
;


# update_wrong_body
#   draw_routine col row
: update_wrong_body
	wrong_guesses make$ length$
	pop		# really should look up the right drawing routine

	"draw_head"	call	# draw_routine
;


: test_update_wrong_body " test_update_wrong_body " .$^
	# arrange
	8873

	test_draw_index 0 @=

	wrong_guesses 0 97 @+=	# wrong guess = 'a'
	wrong_guesses 1 0 @+=		# end string

	# act
	2 3 "test_draw_spy" update_wrong_body

	# assert
	test_draw_area test_draw_index @ 0 @+=
	test_draw_area make$ "2-5-O~" assertEqual$

	8873 assertEqual
;

# draw_partial_body - calls routines in the array, up to but not including the given count
#   In: array count draw_routine col row
var drawing_array
var draw_count
var draw_routine

: draw_partial_body
	drawing_array swap @=	# count draw_routine col row
	draw_count swap @=		# draw_routine col row
	draw_routine swap @=	# col row

	while		# col row
		draw_count @
		0
		!=		# bool col row
	do			# col row
		draw_count @--
		dup2	# col row col row
		draw_routine @	# draw-routine col row col row
		drawing_array @ draw_count @ @+	# part-routine draw_routine col row col row
		call	# col row
	od
			# col row
	pop pop # [empty]
;

5 array test_partial_array

var test_draw_sum

: test_empty_draw_routine ;

: test_draw_helper1 pop pop pop 111 bca ;

: test_draw_helper2 pop pop pop 222 bca ;

: test_draw_partial_body_no_errors " test_draw_partial_body_no_errors " .$^
	# arrange
	22202
	test_draw_sum 0 @=
	test_partial_array 0 "test_draw_helper1" @+=
	test_partial_array 1 "dont_call_this" @+=

	# act
	2 3 "test_empty_draw_routine" 0 test_partial_array draw_partial_body

	# assert
	# no draw_helper was called
	22202 assertEqual
;

: test_draw_partial_body_with_errors " test_draw_partial_body_with_errors " .$^
	# arrange
	22203
	test_draw_sum 0 @=
	test_partial_array 0 "test_draw_helper1" @+=
	test_partial_array 1 "test_draw_helper2" @+=
	test_partial_array 2 "dont_call_this" @+=

	# act
	2 3 "test_empty_draw_routine" 2 test_partial_array draw_partial_body

	# assert
	111 assertEqual
	222 assertEqual
	22203 assertEqual
;
