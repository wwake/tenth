
: test_empty_strings_equal 	" test_empty_strings_equal " .$^
	1234

	"" "" ==$

	1 assertEqual
	1234 assertEqual
;

: test_different_strings_unequal " test_different_strings_unequal " .$^
	2171
	"one" "seven" ==$

	0 assertEqual
	2171 assertEqual
;

# Verifies a fix for a problem when we compared a bool to ==0 rather than the right character
: test_equal_string_underscores
	7892
	"f_sh_" "fishy" ==$

	0 assertEqual
	7892 assertEqual
;

: test_strings test_empty_strings_equal test_different_strings_unequal test_equal_string_underscores ;

#####

var test_inc_var
: test_increment " test_increment " .$^
	43209

	# arrange
	test_inc_var 7 @=

	# act
	test_inc_var @++

	# assert
	test_inc_var @
	8
	assertEqual

	43209 assertEqual
;


: test_title " test_title " .$^
	411
	title 0 @+

	"HANGMAN"

	assertEqual$
	411 assertEqual
;


var test_draw_count

: draw_at_spy	# in: col row string; effect: test_draw_count++, 3 values popped
	test_draw_count @++		# col row string ...
	pop pop pop 			# ...
;

: test_draw " test_draw " .$^
	314159
	test_draw_count 0 @=

	scaffold 10 35 "draw_at_spy" draw

	test_draw_count @ 7 assertEqual
	314159 assertEqual
;

: test_hangman test_title test_draw ;


#####

10 array test_s2a_array

: test_string_to_array_for_empty " test_string_to_array_for_empty " .$^
	14417
	test_s2a_array "" string_to_array

	test_s2a_array 0 @+ 0 assertEqual
	test_s2a_array make$ "" assertEqual$
	14417 assertEqual
;

: test_string_to_array_for_string " test_string_to_array_for_string " .$^
	315
	test_s2a_array "cat"

	string_to_array

	test_s2a_array 0 @+ 99 assertEqual

	test_s2a_array make$ "cat" assertEqual$
	315 assertEqual
;

: test_s2a_tests test_string_to_array_for_empty test_string_to_array_for_string ;



: test_length_of_empty_string " test_length_of_empty_string " .$^
	37281
	"" length$
	0 assertEqual
	37281 assertEqual
;

: test_length_of_nonempty_string " test_length_of_nonempty_string " .$^
	415585
	"rabbit" length$
	6 assertEqual
	415585 assertEqual
;

: test_length_of_strings test_length_of_empty_string test_length_of_nonempty_string ;

###



: test_empty_answer
	37056
	" test_empty_answer " .$^

	"" game_init

	game_over 1 assertEqual
	37056 assertEqual
;

: test_string_answer " test_string_answer " .$^
	8785
	"hi" game_init

	answer make$ "hi" assertEqual$
	clue make$ "__" assertEqual$

	game_over 0 assertEqual
	8785 assertEqual
;

: test_update_clue_first_letter_match " test_update_clue_first_letter_match " .$^
	37172	# guard word
	"cat" game_init

	99 update_clue	# 'c'

	clue make$ "c__" assertEqual$
	game_over 0 assertEqual
	1 assertEqual
	37172 assertEqual
;

: test_update_clue_other_letter_match " test_update_clue_other_letter_match " .$^
	531
	"canal" game_init

	97 update_clue	# 'a'

	clue make$ "_a_a_" assertEqual$
	game_over 0 assertEqual
	1 assertEqual
	531 assertEqual
;

: test_update_clue_wins " test_update_clue_wins " .$^
	37172	# guard word
	"aa" game_init

	97 update_clue	# 'a'

	1 assertEqual			# check update_clue result
	clue make$ "aa" assertEqual$
	game_over 1 assertEqual
	37172 assertEqual
;

: test_calling_string_to_array_multiple_times_doesnt_crash
	34127
	answer "canal"
	string_to_array

	answer "canal"
	string_to_array
	34127 assertEqual
;

: test_update_wrong_guesses " test_update_wrong_guesses " .$^
	135		# guard word
	"fish" game_init

	97 update_wrong_guess	# 'a'
	100 update_wrong_guess	# 'd'
	99 update_wrong_guess	# 'c'

	wrong_guesses make$ "adc" assertEqual$
	135 assertEqual
;

: test_update_clue test_empty_answer test_string_answer test_update_clue_first_letter_match test_update_clue_other_letter_match test_update_clue_wins test_update_wrong_guesses test_calling_string_to_array_multiple_times_doesnt_crash nl
;

#####


10 array read_stub_array
var read_stub_index

: read_stub_init
	read_stub_array swap string_to_array
	read_stub_index 0 @=
;

: read_stub
	read_stub_array read_stub_index @
	@+
	read_stub_index @++
;

: display_stub ;


: test_game_loop " test_game_loop " .$^
	90210
	"aa" game_init
	"ba" read_stub_init

	"display_stub" "read_stub" game_loop

	clue make$ "aa" assertEqual$
	wrong_guesses make$ "b" assertEqual$
	90210 assertEqual
;

: test_game test_game_loop nl ;

#####

50 array test_draw_area
var test_draw_index

# test_draw_spy
# col row string => ...
: test_draw_spy
	swap
	test_draw_area test_draw_index @ cab 48 + @+=	# row (add '0')
	test_draw_index @++

	test_draw_area test_draw_index @ 45 @+=		# write '-'
	test_draw_index @++

	test_draw_area test_draw_index @ cab 48 + @+=	# col (add '0')
	test_draw_index @++

	test_draw_area test_draw_index @ 45 @+=		# write '-'
	test_draw_index @++

	head$
	test_draw_area test_draw_index @ cab @+=	# first char of string
	pop
	test_draw_index @++

	test_draw_area test_draw_index @ 126 @+=	# write '~'
	test_draw_index @++

	test_draw_area test_draw_index @ 0 @+=		# write \0
;

: test_draw_spy_check " test_draw_spy_check " .$^
	1029
	test_draw_index 0 @=

	"*/" 2 4 test_draw_spy

	test_draw_area test_draw_index @ 0 @+=
	test_draw_area make$ "2-4-*~" assertEqual$
	1029 assertEqual
;


: test_draw_head " test_draw_head " .$^
	6738

	test_draw_index 0 @=

	1 2 "test_draw_spy" draw_head

	test_draw_area test_draw_index @ 0 @+=
	test_draw_area make$ "1-4-O~" assertEqual$

	6738 assertEqual
;

: test_draw_torso " test_draw_torso " .$^
	67389

	test_draw_index 0 @=

	1 2 "test_draw_spy" draw_torso

	test_draw_area test_draw_index @ 0 @+=
	test_draw_area make$ "2-4-|~3-4-|~" assertEqual$

	67389 assertEqual
;

: test_draw_left_arm " test_draw_left_arm " .$^
	6738

	test_draw_index 0 @=

	1 2 "test_draw_spy" draw_left_arm

	test_draw_area test_draw_index @ 0 @+=
	test_draw_area make$ "2-3-/~" assertEqual$

	6738 assertEqual
;



: test_update_wrong_body " test_update_wrong_body " .$^
	# arrange
	8873

	test_draw_index 0 @=

	wrong_guesses 0 97 @+=	# wrong guess = 'a'
	wrong_guesses 1 0 @+=		# end string

	# act
	2 3 "test_draw_spy" update_wrong_body

	# assert
	test_draw_area test_draw_index @ 0 @+=
	test_draw_area make$ "2-5-O~" assertEqual$

	8873 assertEqual
;

5 array test_partial_array

var test_draw_sum

: test_empty_draw_routine ;

: test_draw_helper1 pop pop pop 111 bca ;

: test_draw_helper2 pop pop pop 222 bca ;

: test_draw_partial_body_no_errors " test_draw_partial_body_no_errors " .$^
	# arrange
	22202
	test_draw_sum 0 @=
	test_partial_array 0 "test_draw_helper1" @+=
	test_partial_array 1 "dont_call_this" @+=

	# act
	2 3 "test_empty_draw_routine" 0 test_partial_array draw_partial_body

	# assert
	# no draw_helper was called
	22202 assertEqual
;

: test_draw_partial_body_with_errors " test_draw_partial_body_with_errors " .$^
	# arrange
	22203
	test_draw_sum 0 @=
	test_partial_array 0 "test_draw_helper1" @+=
	test_partial_array 1 "test_draw_helper2" @+=
	test_partial_array 2 "dont_call_this" @+=

	# act
	2 3 "test_empty_draw_routine" 2 test_partial_array draw_partial_body

	# assert
	111 assertEqual
	222 assertEqual
	22203 assertEqual
;

: test_drawing
test_draw_spy_check test_draw_head test_draw_torso test_draw_left_arm test_update_wrong_body test_draw_partial_body_no_errors test_draw_partial_body_with_errors
;


#####
: test_all
	test_update_clue
	test_strings
	test_increment
	test_game
	test_hangman
	test_length_of_strings
	test_s2a_tests
	test_drawing
	nl
;
